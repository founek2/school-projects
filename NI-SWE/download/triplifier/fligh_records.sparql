prefix my: <http://localhost/vocabulary/terms/> 
prefix airport: <http://flightrecords.dev/resource/airport/> 
prefix flightrecord: <http://flightrecords.dev/resource/flightrecord/> 
prefix location: <http://flightrecords.dev/resource/location/> 
prefix xsd: <http://www.w3.org/2001/XMLSchema#> 
prefix dbo: <http://dbpedia.org/ontology/>
prefix dbp: <http://dbpedia.org/property/>
prefix geo: <http://www.w3.org/2003/01/geo/wgs84_pos#>
prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
  ?IRI a my:FlightRecord;
    my:callsign ?callsignWithType ;
    my:number ?numerWithType ;
    my:icao24 ?icao24WithType ;
    my:registration ?registrationWithType ;
    my:origin ?IRIorigin ;
    my:destination ?IRIdestination ;
    my:wasSpottedAt ?IRIlocation .
      
  ?IRIorigin a dbo:Airport;
    dbo:icaoLocationIdentifier ?originWithType .

  ?IRIdestination a dbo:Airport;
    dbo:icaoLocationIdentifier ?destinationWithType .
  
  ?IRIlocation a geo:Point ;
    geo:location  ?geometry ;
    geo:lat ?latitudeWithType ;
    geo:long ?longitudeWithType ;
    geo:alt ?altitudeWithType .
}
FROM <file:flightlist_1000.csv>
WHERE {
  BIND (STR(tarql:expandPrefix("flightrecord")) as ?flightRecordNamespace)
  BIND (IRI(CONCAT(?flightRecordNamespace, STR(?callsign))) AS ?IRI)
  BIND (STR(tarql:expandPrefix("airport")) as ?airportNamespace)
  BIND (IRI(CONCAT(?airportNamespace, ?origin)) AS ?IRIorigin )
  BIND (IRI(CONCAT(?airportNamespace, ?destination)) AS ?IRIdestination )
  BIND (STR(tarql:expandPrefix("location")) as ?locationNamespace)
  BIND (IRI(CONCAT(?locationNamespace, STR(?ROWNUM))) AS ?IRIlocation )
  
  BIND (STRDT(?callsign, xsd:string) AS ?callsignWithType)
  BIND (STRDT(?numer, xsd:string) AS ?numerWithType)
  BIND (STRDT(?icao24, xsd:string) AS ?icao24WithType)
  BIND (STRDT(?registration, xsd:string) AS ?registrationWithType)
  BIND (STRDT(?origin, xsd:string) AS ?originWithType)
  BIND (STRDT(?destination, xsd:string) AS ?destinationWithType)

  BIND (STRDT(?latitude_1, xsd:decimal) AS ?latitudeWithType)
  BIND (STRDT(?longitude_1, xsd:decimal) AS ?longitudeWithType)
  BIND (STRDT(CONCAT("POINT(", ?latitude_1, " ", ?longitude_1, ")"), <http://www.openlinksw.com/schemas/virtrdf#Geometry>) AS ?geometry)
  BIND (STRDT(?altitude_1, xsd:decimal) AS ?altitudeWithType)
}
